
plugins {
	id 'findbugs'
}

allprojects {

	repositories {
		maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
		maven { url rootProject.file('lib/') }
		maven { url "http://repo.maven.apache.org/maven2" }
	}

	group = 'eu.aria'
	version = '1.0'


	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'findbugs'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
	}

	// bug finding setup
	findbugs {

		// use this one ... reduce ambiguities
		toolVersion = "3.0.1"
		
		// keep running, even if we've found bugs
		ignoreFailures = true

		// dump (all) the reports here
    reportsDir = file("$rootProject.buildDir/wikibugs/findbugs-$project.name")

		// tell me all the bugs
		reportLevel = "low"

		// find all of the bugs
		effort = "max"
	}
	
	// tweak the task to write XML
	tasks.withType(FindBugs) {
    reports {
        xml.enabled true
        html.enabled false
    }
	}
}

task run(type: JavaExec, dependsOn: [classes, test]) {
	description = "Run the command line program"
	args = [
	]

	main = 'eu.aria.dm.Main'
	classpath = sourceSets.main.runtimeClasspath
}

project(':DM_Tools:QAM') {

	version = '0.1'

	dependencies {
		compile 'org.apache.commons:commons-lang3:3.3.1'
		compile 'commons-io:commons-io:2.5'
	}
}

dependencies {
	compile 'com.github.mpkorstanje:simmetrics-core:4.1.1'
	compile 'ch.qos.logback:logback-classic:1.2.3'
	compile 'org.json:json:20160807'
	compile 'javax.json:javax.json-api:1.0'
	compile group: 'edu.stanford.nlp', name: 'stanford-corenlp', version:'3.7.0', classifier:'models'

	compile project(':DM_Tools:QAM')

	compile 'eu.aria.util:Input-Types:0.1'
	compile(group: 'hmi.flipper', name: 'flipper2', version:'0.1') {
		exclude(module: 'slf4j-log4j12')
		exclude(module: 'log4j')
	}
	compile(group: 'eu.aria.util', name: 'Translator', version:'0.1') {
		exclude(module: 'slf4j-log4j12')
	}
	
	compile 'edu.stanford.nlp:stanford-corenlp:3.7.0'
	compile 'javax.jms:javax.jms-api:2.0.1'
	compile 'org.glassfish:javax.json:1.0.4'
	compile 'org.jeasy:easy-rules-core:3.0.0'
	
	testCompile 'org.hamcrest:hamcrest-core:1.3'
	testCompile 'junit:junit:4.12'
	
	compile 'com.squareup.okhttp3:okhttp:3.9.1'
	compile 'dom4j:dom4j:1.1'
	compile(group: 'org.apache.activemq', name: 'activemq-all', version:'5.15.1') {
		exclude(module: 'slf4j-log4j12')
		exclude(module: 'log4j')
	}
	
}
